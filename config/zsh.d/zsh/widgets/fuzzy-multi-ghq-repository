# Source dive multiple ghq managed repositories.

emulate -L zsh

if (( ! $+commands[fzf] )) && (( ! $+commands[ghq] )); then
  return 1
fi

typeset -g FZF_EDITOR
declare -a repositories
local repository opts fullpath working_directory

opts="
$FZF_DEFAULT_OPTS
-0 -m --ansi --select-1 --height 70%
--no-preview --delimiter=/ --nth=2,3,4,5
--extended-exact --bind='ctrl-s:toggle-sort'
"

repositories=$(ghq list | FZF_DEFAULT_OPTS="$opts" fzf)
repositories=( ${(f)repositories} )

if [[ -n "$repositories" ]]; then
  # pushd will change to the last repository's directory which is
  # not desired. Save the working directory and change to it later.
  working_directory=$PWD
  for repository in "${repositories[@]}"; do
    fullpath=$(ghq list -e -p $repository)
    # Add repository path to directory stack.
    pushd -q $fullpath
    ${FZF_EDITOR:-$EDITOR} "$fullpath"
  done
  cd -- $working_directory
  zle accept-line
fi
